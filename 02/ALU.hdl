// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:

    /********************************** X MANIPULATIONS **********************/

    /** zero the x if zx = true**/
    Not16(in[0..15]=true, out=zeroedX);
    Mux16(a=x, b=zeroedX, sel=zx, out=xAfterZeroing);

    /** negate the x if nx = true **/
    Not16(in[0..15]=xAfterZeroing, out=negatedX);
    Mux16(a=xAfterZeroing, b=negatedX, sel=nx, out=xAfterNegation);

    /********************************* Y MANIPULATIONS **********************/

    /** zero the y if zy = true **/
    Not16(in[0..15]=true, out=zeroedY);
    Mux16(a=y, b=zeroedY, sel=zy, out=yAfterZeroing);

    /** negate the y if ny = true **/
    Not16(in[0..15]=yAfterZeroing, out=negatedY);
    Mux16(a=yAfterZeroing, b=negatedY, sel=ny, out=yAfterNegation);

    /****************************** COMPUTE OUT *******************************/

    Add16(a=xAfterNegation, b=yAfterNegation, out=xPlusY);
    And16(a=xAfterNegation, b=yAfterNegation, out=xAndY);
    Mux16(a=xAndY, b=xPlusY, sel=f, out=computationResult);

    /***************************** NEGATE OR NOT *********************************/
    
    Not16(in=computationResult, out=neggedResult);
    Mux16(a=computationResult, b=neggedResult, sel=no, out=out, out[15]=ng, out=finalResult, out[0..7]=partialResult, out[8..15]=partialResult2);

    /***************************** GET ZR *************************************/
    
    Or8Way(in=partialResult, out=hasZeroes1);
    Or8Way(in=partialResult2, out=hasZeroes2);
    Or(a=hasZeroes1, b=hasZeroes2, out=tempZr);
    Not(in=tempZr, out=zr);
   
}